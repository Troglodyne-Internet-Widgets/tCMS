#!/usr/bin/env perl

use strict;
use warnings;

use FindBin::libs;
use Trog::Config;
use Archive::Tar::Builder;
use PerlIO::gzip;
use File::Slurper;
use File::Copy;

my $contactemail = $ARGV[0];
die "Must pass contact email." unless $contactemail;

# use bin/tcms_hostname to get the hostname, and then build a data.tar.gz based on the existing data
my $conf = Trog::Config::get();
my $hostname = Trog::Config::hostname($conf);
my $username = Trog::Config::hostname($conf, undef, 1);

my $THIS_DIR="$Trog::Config::home_dir/provisioner_configs";

mkdir $hostname unless -d $hostname;

# Figure out what to backup
my %to_archive = map {
    my $subj = $_;
    "$Trog::Config::home_dir/$subj" => $subj
} grep {
    my $subj = $_;
    -d "$Trog::Config::home_dir/$subj"
} qw{config data www/themes www/assets};

my $written_template = "$THIS_DIR/Makefile";
File::Copy::copy("$THIS_DIR/Makefile.tmpl", $written_template);
my $template = File::Slurper::read_text($written_template);
$template =~ s/%TCMS_HOSTNAME%/$hostname/gmx;
$template =~ s/%TCMS_USERNAME%/$username/gmx;
File::Slurper::write_text($written_template, $template);

$to_archive{"$THIS_DIR/Makefile"} = 'Makefile';

# Build tar file
open(my $th, '>', "$hostname/data.tar");
my $tar = Archive::Tar::Builder->new();
$tar->set_handle($th);
$tar->archive_as(%to_archive);
$tar->finish();
close $th;

# XXX Unfortunately ATB doesn't play well with PIO::Gzip.
# This is VERY BAD, as we have to do an unnecessary read of a potentially huge tar now.
# This is because it doesn't use the standard perl means for doing buffered I/O.
#open(my $zh, '>:gzip', "$hostname/data.tar.gz");
#print $zh File::Slurper::read_binary("$hostname/data.tar");
#close($zh);
#unlink "$hostname/data.tar";

# So, we're gonna shell out for lack of better options.
system(qw{pigz -f}, "$hostname/data.tar");

# Now that we have the backup, we just have to make the config files.
my $pconf = File::Slurper::read_text("$THIS_DIR/provision.tmpl");
$pconf =~ s/%CONTACTEMAIL%/$contactemail/gmx;
File::Slurper::write_text("$THIS_DIR/$hostname/provision.conf", $pconf);

print "Done. Please put your administrative user configuration in $THIS_DIR/$hostname/users.yaml\n" unless -f "$THIS_DIR/$hostname/users.yaml";
